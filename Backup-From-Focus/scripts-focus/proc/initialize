#
# This is not an executable script.
#
# This should be called at the beginning of each 2dx script, to initialize the script environment.
#
echo ":++++++++++++++++++++++++++++++++"
echo ":Initializing script environment"
echo ":++++++++++++++++++++++++++++++++"
#
echo ": "
echo ": Running the following shell:"
echo ": " `set | grep version`
echo ": "
#
set date = `date`
echo date = ${date}
set system = `uname -s`
echo system = ${system}
#
echo bin_2dx = ${bin_2dx}
echo proc_2dx = ${proc_2dx}
#
# The paths like app_python, app_anaconda comes from the 2DX Preferences set
# by the user. 
#
if ( ! $?app_python ) then
  source ~/.2dx/2dx.cfg
endif
#
###################################
# Initialize IMOD 
###################################
if (-e ${dir_imod}/IMOD-mac.csh ) source ${dir_imod}/IMOD-mac.csh
#
if (-e ${dir_imod}/IMOD-linux.csh ) source ${dir_imod}/IMOD-linux.csh
#
#
#
#
#
#
#
###################################
# Initialize PYTHON
###################################
if ( $?app_python ) then
  if ( ! -e ${app_python} ) then
    echo ":: "
    ${proc_2dx}/linblock "Python executable not found. Is it installed ???"
    ${proc_2dx}/linblock "${app_python}"
    ${proc_2dx}/linblock "The location of python is defined in Preferences."
    echo ":: "
  endif
  if ( $?noEMAN ) then
   if ( ${noEMAN} != "y" ) then
    if ( ! -d ${dir_eman2} ) then
      echo ":: "
      ${proc_2dx}/linblock "EMAN2 installation directory not found. Is it installed ???"
      ${proc_2dx}/linblock "${dir_eman2}"
      ${proc_2dx}/linblock "The location of EMAN2 installation is defined in Preferences."
      echo ":: "
    else
      set EMAN2_dir = ${dir_eman2}
      set path=($path ${EMAN2_dir}/bin ${EMAN2_dir}/extlib/bin)
      if ( $?PYTHONPATH ) then
      else
          setenv PYTHONPATH
      endif
      setenv PYTHONPATH ${EMAN2_dir}/lib:${EMAN2_dir}/bin:${EMAN2_dir}/extlib/site-packages:${EMAN2_dir}
    endif
    ${proc_2dx}/lin "dir_eman2 = ${dir_eman2}"
   endif
  else
    set EMAN2_dir = ${dir_eman2}
    set path=($path ${EMAN2_dir}/bin ${EMAN2_dir}/extlib/bin)
    if ( $?PYTHONPATH ) then
    else
        setenv PYTHONPATH
    endif
    setenv PYTHONPATH ${EMAN2_dir}/lib:${EMAN2_dir}/bin:${EMAN2_dir}/extlib/site-packages:${EMAN2_dir}
    ${proc_2dx}/lin "dir_eman2 = ${dir_eman2}"
  endif
  #
else
  echo "::WARNING: CSH Variable app_python is not defined. You may be running this script from the wrong environment."
endif
#
${proc_2dx}/lin "app_python = ${app_python}"
${proc_2dx}/lin "app_anaconda = ${app_anaconda}"
#
#
#
setenv PYTHONPATH ${PYTHONPATH}:/home/henning/P/scripts/proc
#
#
if ( ! $?dir_frealign ) then
  set dir_frealign = "none"
endif
${proc_2dx}/lin "dir_frealign = ${dir_frealign}"
#
${proc_2dx}/lin "dir_relion = ${dir_relion}"
if ( ! -d ${dir_relion} ) then
  echo ":: "
  ${proc_2dx}/linblock "RELION installation directory not found. Is it installed ???"
  ${proc_2dx}/linblock "${dir_relion}"
  ${proc_2dx}/linblock "The location of RELION installation is defined in Preferences."
  echo ":: "
else
  set path=(${dir_relion}/bin $path)
endif

alias sparx sx.py
##
#
#
#
#

####################################################
# Find CCP4 CSH setup file and source it!
###################################################
if ( $?ccp4_setup ) then
  set IS_2DX = yes
  if ( $?dir_ccp4 ) then
    if ( ! -d ${dir_ccp4} ) then
      echo ":: "
      ${proc_2dx}/linblock "CCP4 installation directory not found. Is it installed ???"
      ${proc_2dx}/linblock "${dir_ccp4}"
      ${proc_2dx}/linblock "The location of CCP4 installation is defined in Preferences."
      echo ":: "
      set ccp4 = "NOTFOUND"
      set bin_ccp4 = "NOTFOUND"
    else
      set ccp4 = ${dir_ccp4}
      set bin_ccp4 = ${ccp4}/bin
    endif
    ${proc_2dx}/lin "dir_ccp4 = ${dir_ccp4}"
  else
    echo ":: "
    echo ":: "
    ${proc_2dx}/linblock "ERROR: Variable dir_ccp4 is not defined ???"
    echo ":: "
    echo ":: "
  endif
  if ( ${ccp4_setup} == 'y' ) then
    if ( ( ! -d ${ccp4} ) || ( ! -d ${bin_ccp4} ) ) then
      echo ":: "
      ${proc_2dx}/linblock "CCP4 not found. Is it installed ???"
      echo ":: "
      echo ":: Is this correct: ???"
      echo ":: ccp4 base directory = ${ccp4}"
      echo ":: ccp4 bin  directory = ${bin_ccp4}"
      echo ":: "
      echo ":: "
      ${proc_2dx}/linblock "The location of CCP4 is defined in the preferences button in the GUI top right."
      ${proc_2dx}/linblock "This is then used to initialize CCP4 in ${proc_2dx}/initialize."
      echo ":: "
      ${proc_2dx}/linblock "You will not be able to create a final map without CCP4."
      echo ":: "
      ${proc_2dx}/linblock "Install CCP4 from http://www.ccp4.ac.uk/download"
      echo ":: "
    else
      # Set the CCP4 CSH setup script
      echo "Finding CSH setup script from CCP4 installation.. "
      set ccp4_setup = ${bin_ccp4}/ccp4.setup-csh
      if ( ! -e ${ccp4_setup} ) then
        set ccp4_setup = ${ccp4}/include/ccp4.setup-csh
        if ( ! -e ${ccp4_setup} ) then
	  echo ":: WARNING: CSH setup script from CCP4 installation not found. Trying other setup scripts."
          set ccp4_setup = ${ccp4}/include/ccp4.setup
        endif
      endif
      if ( -e ${ccp4_setup} ) then
        # This is sourcing a script within a sourced script. Hopefully, this will work...
        echo ":   Sourcing ${ccp4_setup}"
        source ${ccp4_setup}
        echo ":   Finished with ${ccp4_setup}"
      else
        ${proc_2dx}/linblock "WARNING: ${ccp4_setup} does not exists."
        echo ":: "
        echo ":: Is this correct: ???"
        echo ":: ccp4 base directory = ${ccp4}"
        echo ":: ccp4 bin  directory = ${bin_ccp4}"
        echo ":: "
        echo ":: "
        ${proc_2dx}/linblock "CCP4 setup file not found. Is CCP4 correctly installed ???"
        ${proc_2dx}/linblock "The location of CCP4 is defined in ${proc_2dx}/initialize."
        ${proc_2dx}/linblock "You can edit it there."
        ${proc_2dx}/linblock "CCP4 could for example be installed in /usr/local/ccp4"
        echo ":: "
        ${proc_2dx}/linblock "You will not be able to create a final map without CCP4."
        echo ":: "
        ${proc_2dx}/linblock "Please make sure that that file can be found."
        echo ":: "
      endif
    endif
  endif
endif
#
source ${proc_2dx}/2dx_makedirs
#
#
#




